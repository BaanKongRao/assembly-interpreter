        lw 0 1 n ;load n to reg 1
        lw 0 2 r ;load r to reg 2
        lw 0 6 pnrAd ;load permutation to reg 6
        jalr 6 7 ;call permutation
        halt ;stop program
pnr     lw 0 6 two ;load 2 into reg 6
        add 5 6 5 ;allocate 2 stack slot
        sw 5 7 zero ;store return address to stack
        lw 0 6 neg1 ;load -1 to reg 6
        add 5 6 6 ;calculate stack slot
        sw 6 1 zero ;store n to stack
        beq 2 0 ret1 ;check if r = 0 then return 1
        lw 0 6 neg1 ;load -1 to reg 6
        add 1 6 1 ;minus reg 1 (n) by 1
        add 2 6 2 ;minus reg 2 (r) by 1
        lw 0 6 pnrAd ;load permutation
        jalr 6 7 ;call permutation
        lw 0 6 neg1 ;load -1 to reg 6
        add 5 6 6 ;calculate stack slot
        lw 6 1 zero ;load n to reg 1
        add 3 0 2 ;copy result to reg 2
        lw 0 6 mulAd ;load multiplication
        jalr 6 7 ;call multiplication
        lw 5 7 zero ;load return address to reg 7
        lw 0 6 neg2 ;load -1 to reg 6
        add 5 6 5 ;release stack
        jalr 7 0 ;return
ret1    lw 5 7 zero ;load return address to reg 7
        lw 0 6 neg2 ;load -2 to reg 6
        add 5 6 5 ;release stack
        lw 0 3 one ;load 1 to reg 3(return value)
        jalr 7 0 ;go back
mul     lw 0 6 one ;load 1 to reg 6
        add 5 6 5 ;allocate 1 stack slot
        sw 5 7 zero ;store return address into stack
        lw 0 3 zero ;reset reg 3
        lw 0 4 bitC ;load bit count to reg 4
        lw 0 7 one ;load pivot to reg 7
mLoop   beq 4 0 mRet ;if bit count equal 0 return
        lw 0 6 neg1 ;load -1 to reg 6
        add 4 6 4 ;minus bit count by 1
        nand 1 7 6 ;nand multiplicand and pivot to check bit
        nand 6 6 6 ;nand result with itself to flip bit
        beq 6 0 mSkip ;skip if result equal 0
        add 3 2 3 ;add multiplier to result
mSkip   add 7 7 7 ;shift pivot left
        add 2 2 2 ;shift multiplier left
        lw 0 6 mlpAd ;load loop for call
        jalr 6 0 ;loop
mRet    lw 5 7 zero ;load return address from stack
        lw 0 6 neg1 ;load -1 to reg 6
        add 5 6 5 ;release stack
        jalr 7 0 ;return
n       .fill 5
r       .fill 3
two     .fill 2
one     .fill 1
zero    .fill 0
neg1    .fill -1
neg2    .fill -2
pnrAd   .fill pnr
bitC    .fill 33
mulAd   .fill mul
mlpAd   .fill mLoop